import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	apply from: 'dependencies.gradle'
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.1.10'
	id 'java'
}

def supportedJava = 21

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(supportedJava))
}

kotlin {
	jvmToolchain {
		languageVersion.set(JavaLanguageVersion.of(supportedJava))
	}
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-source', '' + supportedJava, '-target', '' + supportedJava]
}

compileTestJava.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()

	// Spigot
	maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

	// Bungeecord
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }

	// Paper, FAWE and Adventure API
	maven { url 'https://repo.papermc.io/repository/maven-public/' }

	// WorldEdit
	maven { url 'https://maven.enginehub.org/repo/' }

	// Skript
	maven { url 'https://repo.skriptlang.org/releases' }

}

dependencies {
	implementation "org.spigotmc:spigot-api:${project.spigot_version}"
	implementation "org.jetbrains.kotlin:kotlin-stdlib:${project.kotlin_version}"
	implementation ("com.sk89q.worldedit:worldedit-bukkit:${project.worldedit_version}") {
		exclude module: 'bstats-bukkit'
	}

	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: project.skript_version) {
		transitive = false
	}

	// FastAsyncWorldEdit
	implementation(platform("com.intellectualsites.bom:bom-newest:${project.fawe_version}"))
	compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
	compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit") { transitive = false }
}

processResources {
	filter ReplaceTokens, tokens: [
		'kotlin_version' : project.property('kotlin_version'),
		'version' : project.property('plugin_version'),
	]
}

jar {
	archiveVersion.set(project.plugin_version)
}
